CREATE OR REPLACE TRIGGER PEADMIN.TRG_PERSONAL_STATUS
  AFTER INSERT OR UPDATE OF C_UPGRADE_DATE, C_UPGRADE_REASON, C_TYPE, C_POSITION, C_REGBELONG, C_PARTY, C_GRADE, C_SCHOOLHISTORY 
  ON T_PERSONAL  FOR EACH ROW
BEGIN
--1.删除新的C_UPGRADE_DATE后有效的记录
 DELETE FROM T_PSN_STATUS 
  WHERE C_BELONG = :NEW.C_BELONG
      AND C_STATUSPSN = :NEW.C_PERSONALID
   AND C_ONDATE >= :NEW.C_UPGRADE_DATE;
--2.修改当前有效时间内的C_DOWNDATE为新的C_UPGRADE_DATE
  UPDATE T_PSN_STATUS SET C_DOWNDATE = (:NEW.C_UPGRADE_DATE - 1)
  WHERE C_BELONG = :NEW.C_BELONG
      AND C_STATUSPSN = :NEW.C_PERSONALID 
   AND C_ONDATE = (
                      SELECT MAX(C_ONDATE) 
                      FROM T_PSN_STATUS 
                      WHERE C_BELONG = :NEW.C_BELONG 
                      AND C_STATUSPSN = :NEW.C_PERSONALID 
                      AND C_ONDATE < :NEW.C_UPGRADE_DATE);
--3.插入新的状态变动记录
  INSERT INTO T_PSN_STATUS (
  C_BELONG,
  C_STATUSPSN,
  C_ONDATE,
  C_UPGRADE_REASON,
  C_TYPE,
  C_POSITION,
  C_REGBELONG,
  C_PARTY,
  C_GRADE,
  C_SCHOOLHISTORY,
  C_DOWNDATE,
  C_DOPSN
  )
  VALUES
    (:NEW.C_BELONG,
    :NEW.C_PERSONALID,
    :NEW.C_UPGRADE_DATE,
    :NEW.C_UPGRADE_REASON, 
    :NEW.C_TYPE, 
    :NEW.C_POSITION, 
    :NEW.C_REGBELONG, 
    :NEW.C_PARTY, 
    :NEW.C_GRADE, 
    :NEW.C_SCHOOLHISTORY,
    TO_DATE('9999/12/31', 'YYYY/MM/DD'),
  :NEW.C_LASTMODIFIER);
END;
/
